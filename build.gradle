subprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'

  repositories {
      jcenter()
  }

  dependencies {
      testCompile 'junit:junit:4.12'
      testCompile 'org.hamcrest:hamcrest-all:1.3'
  }

  sourceSets {
    acceptanceTest {
      java {
        srcDir file('../acceptanceTests/src')
        compileClasspath += main.output
        runtimeClasspath += main.output
      }
    }
  }

  configurations {
    acceptanceTestCompile.extendsFrom testCompile
    acceptanceTestRuntime.extendsFrom testRuntime
  }

  task acceptanceTest(type: Test) {
    testClassesDir = project.sourceSets.acceptanceTest.output.classesDir
    classpath = project.sourceSets.acceptanceTest.runtimeClasspath
  }

  checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    checkstyleTest.enabled = false
    checkstyleAcceptanceTest.enabled = false
    toolVersion = '7.1.2'
  }

  task execute(type:JavaExec) {
    if (project.hasProperty('mainClass')) {
      main = mainClass
    } else {
      main = 'Main'
    }
    classpath = sourceSets.main.runtimeClasspath
  }
}
